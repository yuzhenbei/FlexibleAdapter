allprojects {
    // Top-level build file where you can add configuration options common to all sub-projects/modules.
    ext {
        // Author
        developerId = "davideas"
        developerName = "Davide Steduto"
        developerEmail = "dave.dna@gmail.com"

        // Library Repository
        libraryName = "FlexibleAdapter"
        bintrayRepo = "maven"
        packageExt = "aar"
        siteUrl = "https://github.com/davideas/FlexibleAdapter"
        gitUrl = "https://github.com/davideas/FlexibleAdapter.git"

        // SDK and Build tools version
        minSdk = 14
        targetSdk = 28 // Android Pie
        buildTools = "28.0.3"

        // Android Architecture Components
        archLifecycleVer = "1.1.1"
        archExtensionsVer = "1.1.1"
        roomPersistenceVer = "1.1.0-beta2"
        archLib = [
                lifecycle      : "android.arch.lifecycle:runtime:${archLifecycleVer}",
                extensions     : "android.arch.lifecycle:extensions:${archExtensionsVer}",
                compiler       : "android.arch.lifecycle:compiler:${archExtensionsVer}",
                commonJava8    : "android.arch.lifecycle:common-java8:${archExtensionsVer}",
                reactiveStreams: "android.arch.lifecycle:reactivestreams:${archExtensionsVer}",
                coreTesting    : "android.arch.core:core-testing:${archExtensionsVer}",
                room           : "android.arch.persistence.room:runtime:${roomPersistenceVer}",
                roomCompiler   : "android.arch.persistence.room:compiler:${roomPersistenceVer}",
                roomRxJava2    : "android.arch.persistence.room:rxjava2:${roomPersistenceVer}"
        ]

        // AndroidX
        androidxVer = "1.0.0"
        lifecycleVer = "2.0.0"
        constraintVer = "1.1.2"
        androidx = [
                recyclerView        : "androidx.recyclerview:recyclerview:${androidxVer}",
                appCompat           : "androidx.appcompat:appcompat:${androidxVer}",
                cardView            : "androidx.cardview:cardview:${androidxVer}",
                annotation          : "androidx.annotation:annotation:${androidxVer}",
                lifecycleExtensions : "androidx.lifecycle:lifecycle-extensions:${lifecycleVer}",
                constraintLayout    : "com.android.support.constraint:constraint-layout:${constraintVer}"
        ]

        // Google Material
        googleVer = "1.0.0"
        google = [
                material        : "com.google.android.material:material:${androidxVer}"
        ]

        // Widgets
        flipViewVer = "1.1.3@aar"
        iconicsVer = "3.1.0@aar"
        iconicsCmdVer = "2.7.94.1@aar"
        widgets = [
                flipView        : "eu.davidea:flipview:${flipViewVer}",
                iconicsCore     : "com.mikepenz:iconics-core:${iconicsVer}",
                iconicsViews    : "com.mikepenz:iconics-views:${iconicsVer}",
                iconicsCommunity: "com.mikepenz:community-material-typeface:${iconicsCmdVer}",
        ]

        // 3rd Libraries
        butterKnifeVer = "9.0.0-SNAPSHOT"
        timberVer = "4.7.1"
        glideVer = "4.6.1"
        javaxInjectVer = "1"
        junitPlatformVer = "1.1.0"
        junitJupiterVer = "5.2.0"
        libraries = [
                butterKnife        : "com.jakewharton:butterknife:${butterKnifeVer}",
                butterKnifeCompiler: "com.jakewharton:butterknife-compiler:${butterKnifeVer}",
                timber             : "com.jakewharton.timber:timber:${timberVer}",
                glide              : "com.github.bumptech.glide:glide:${glideVer}",
                glideCompiler      : "com.github.bumptech.glide:compiler:${glideVer}",
                javaxInject        : "javax.inject:javax.inject:${javaxInjectVer}",
        ]

        // License stuff
        licenseName = "The Apache Software License, Version 2.0"
        licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        licenseDist = "repo"
        allLicenses = ["Apache-2.0"]
    }

    repositories {
        jcenter()
        google()
        maven { url "http://dl.bintray.com/davideas/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

buildscript {
    repositories {
        jcenter()
        google()
        maven { url "http://dl.bintray.com/davideas/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1"
        classpath "com.vanniktech:gradle-android-javadoc-plugin:0.3.0-SNAPSHOT"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.2.0.0"
        classpath "eu.davidea:grabver:1.0.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}